* TODO: merge with ChPTdiagram_external.hf

#procedure setkaons(EXPR,NKPAIR)

    .sort
    skip; nskip `EXPR';

    #if `NKPAIR'>0

        functions <K1p,K1m>,...,<K`NKPAIR'p,K`NKPAIR'm>;
        set Kx:   <K1p,K1m>,...,<K`NKPAIR'p,K`NKPAIR'm>;
        symbols   <S1>,...,<S`NKPAIR'>;

        multiply assign(<f1>,...,<f`NEXT'>);
        #do I=1,`NKPAIR'
            id assign(?a) = distrib_(1,2, kaons,assign, ?a);
            id kaons(i?,j?) = replace_(i,K`I'p(i)) * replace_(j,K`I'm(j));
        #enddo
        id assign(?a) = 1;

        .sort
        skip; nskip `EXPR';

        #do I=1,`NKPAIR'
            tryreplace K`I'p,K`I'm, K`I'm,K`I'p;
        #enddo

        .sort
        skip; nskip `EXPR';

    #endif

    repeat;
        id Tr(?a, 8, f1?extflav, ?b) = Tr(?a, f1, ?b) / sqrt6;
        id Tr(?a, f1?extflav, 8, ?b) = Tr(?a, f1, ?b) / sqrt6;
    endrepeat;
    id Tr(f1?extflav) = 0;
    #if `NKPAIR'>0
        #do I=1,`NKPAIR'
            repeat;
                id Tr(?a, 8, K`I'p(f1?), ?b) = Tr(?a, K`I'p(f1), ?b) * (1 + 3/2*(1 + S`I')) / sqrt6;
                id Tr(?a, 8, K`I'm(f1?), ?b) = Tr(?a, K`I'm(f1), ?b) * (1 + 3/2*(1 - S`I')) / sqrt6;
            endrepeat;
            repeat;
                id Tr(?a, K`I'p(f1?), 8, ?b) = Tr(?a, K`I'p(f1), ?b) * (1 - 3/2*(1 - S`I')) / sqrt6;
                id Tr(?a, K`I'm(f1?), 8, ?b) = Tr(?a, K`I'm(f1), ?b) * (1 - 3/2*(1 + S`I')) / sqrt6;
            endrepeat;
        #enddo
    #endif
    id Tr(K1p?Kx(f1?)) = 0;

    .sort

#endprocedure
